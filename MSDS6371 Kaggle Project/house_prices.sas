*https://blogs.sas.com/content/sasdummy/2013/06/12/correlations-matrix-heatmap-with-sas/;

%MACRO READ_DATA(PATH,NAME);
* REPLACE NA WITH MISSING;
DATA _NULL_;
	INFILE "&PATH.&NAME..csv" DSD TRUNCOVER;
	FILE "&PATH.&NAME._missing.csv" DSD;
	LENGTH WORD $200;
	
	* LOOP THROUGH EACH OF THE 81 COLUMNS AND REPLACE 'NA' WITH .;
	DO I=1 TO 81;
		INPUT WORD @;
		IF I IN (7,31,32,33,34,36,58,59,61,64,65,73,74,75) THEN DO;
			PUT WORD@;
		END;
		ELSE DO;
			IF WORD='NA' THEN WORD = .;
			PUT WORD@;
		END;
	END;
	
	* OUTPUT THE RECORD TO THE FILE;
	PUT;
RUN;

* IMPORT THE FILE WHERE NAs ARE REPLACED WITH . ;
PROC IMPORT DATAFILE="&PATH.&NAME._missing.csv" OUT=&NAME REPLACE;
	GUESSINGROWS=MAX;
RUN;
%MEND;

* CREATE A DATASET WITH THE LIST OF FILES TO READ IN;
DATA LIST;
	* THIS IS WHERE YOU UPDATE WITH THE LOCATION OF YOUR FILES;
	PATH='/data/bnsf/ib/hubops/jford/data_science/kaggle/';
	NAME='train'; OUTPUT;
	NAME='test'; OUTPUT;
RUN;

* EXECUTE THE MACRO TO LOOP THROUGH THE FILES BEING READ IN;
DATA _NULL_;
	SET LIST;
    CALL EXECUTE('%READ_DATA('||PATH||','||NAME||')');
RUN;

DATA TRAIN1;
SET TRAIN;
IF NEIGHBORHOOD IN ('Edwards','BrkSide','NAmes');
IF NEIGHBORHOOD = 'Edwards' THEN DO;
	N1=1; 
	N2=0;
END;

ELSE IF NEIGHBORHOOD = 'BrkSide' THEN DO;
	N1=0;
	N2=1;
END;

ELSE DO;
	N1=0;
	N2=0;
END;

SALEPRICE_LOG = LOG(SALEPRICE);
GRLIVAREA_LOG = LOG(GRLIVAREA/100);

INT1 = N1 * GRLIVAREA_LOG;
INT2 = N2 * GRLIVAREA_LOG;
RUN;

SYMBOL1 V='SQUARE' C=BLACK I=NONE;
SYMBOL2 V='CIRCLE' C=RED I=NONE;
SYMBOL3 V='TRIANGLE' C=BLUE I=NONE;
TITLE 'House Sales Price and Sq Footage by Neighborhood';

PROC GPLOT DATA=TRAIN1;
PLOT SALEPRICE*GRLIVAREA=NEIGHBORHOOD;
RUN;

PROC GPLOT DATA=TRAIN1;
PLOT SALEPRICE_LOG*GRLIVAREA_LOG=NEIGHBORHOOD;
RUN;

ODS GRAPHICS ON;
PROC REG DATA=TRAIN1;
MODEL SALEPRICE = N1 N2 GRLIVAREA /VIF; /*VIF SHOULD BE BELOW 10, IDEALLY CLOSE TO 1*/
TITLE 'REGULAR';
RUN;

PROC REG DATA=TRAIN1;
MODEL SALEPRICE_LOG = N1 N2 GRLIVAREA /VIF; /*VIF SHOULD BE BELOW 10, IDEALLY CLOSE TO 1*/
TITLE 'LOG-LINEAR';
RUN;

PROC REG DATA=TRAIN1;
MODEL SALEPRICE = N1 N2 GRLIVAREA_LOG /VIF; /*VIF SHOULD BE BELOW 10, IDEALLY CLOSE TO 1*/
TITLE 'LINEAR-LOG';
RUN;

PROC REG DATA=TRAIN1;
MODEL SALEPRICE_LOG = N1 N2 GRLIVAREA_LOG /VIF; /*VIF SHOULD BE BELOW 10, IDEALLY CLOSE TO 1*/
TITLE 'LOG-LOG';
RUN;

PROC REG DATA=TRAIN1;
MODEL SALEPRICE_LOG = N1 N2 GRLIVAREA_LOG INT1 INT2/VIF; /*VIF SHOULD BE BELOW 10, IDEALLY CLOSE TO 1*/
TITLE 'LOG-LOG';
RUN;

PROC MEANS DATA=TRAIN1;
RUN;

DATA TRAIN2;
SET TRAIN1;
CENT1 = (GRLIVAREA_LOG - 7.1193132)*(N1 - 0.2610966);
CENT2 = (GRLIVAREA_LOG - 7.1193132)*(N2 - 0.1514360);
RUN;

* THIS IS THE MODEL WE WANT;
PROC REG DATA=TRAIN2;
MODEL SALEPRICE_LOG = N1 N2 GRLIVAREA_LOG CENT1 CENT2/VIF CLB CLM; /*VIF SHOULD BE BELOW 10, IDEALLY CLOSE TO 1*/
TITLE 'LOG-LOG';
RUN;
